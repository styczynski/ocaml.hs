IRoot.          Implementation      ::= ImplPhrase ;
IRootComplex.   Implementation      ::= Implementation Implementation ;
internal INothing.       Implementation      ::= "" ;

IPhrase.        ImplPhrase          ::= Expression ;
IDef.           ImplPhrase          ::= Definition ;
IMacro.         ImplPhrase          ::= BuiltinMacro ;

MacHSSuite.     BuiltinMacro        ::= "[@@@ocamlhs_suite]" Implementation "[@@@ocamlhs_suite_test]" [Constant] "[@@@end ocamlhs_suite]";

DefLet.         Definition          ::= "let" Pattern "=" Expression ;
DefLetFun.      Definition          ::= "let" Pattern [Pattern] "=" Expression ;

PatIdent.       Pattern             ::= Ident ;
PatConst.       Pattern             ::= Constant ;
PatNested.      Pattern             ::= "(" Pattern ")" ;
PatNone.        Pattern             ::= "_" ;

separator       Pattern             "" ;

EConst.         Expression          ::= Constant ;
EIdent.         Expression          ::= Ident ;
EComplex.       Expression          ::= NAryExpression ;
EParens.        Expression          ::= "(" Expression ")" ;
separator       Expression          "" ;

ENCall.         NAryExpression      ::= Ident [Expression] ;
ENPrefix.       NAryExpression      ::= PrefixOperator Expression ;
ENInfix.        NAryExpression      ::= Expression InfixOperator Expression ;
ENLet.          NAryExpression      ::= "let" Pattern "=" Expression "in" Expression ;
ENIf.           NAryExpression      ::= "if" Expression "then" Expression "else" Expression ;
ENWhile.        NAryExpression      ::= "while" Expression "do" Expression "done" ;
ENFor.          NAryExpression      ::= "for" Ident "=" Expression ForDirection Expression "do" Expression "done" ;

ForDirTo.       ForDirection        ::= "to" ;
ForDirDownTo.   ForDirection        ::= "downto" ;

CInt.           Constant            ::= Integer ;
CString.        Constant            ::= String ;
CBool.          Constant            ::= ConstantBool ;
separator       Constant            "" ;

CBTrue.         ConstantBool        ::= "true" ;
CBFalse.        ConstantBool        ::= "false" ;

OPAnd.          InfixOperator       ::= "&&" ;
OPOr.           InfixOperator       ::= "||" ;
OPSemicolon.    InfixOperator       ::= ";" ;
OPPlus.         InfixOperator       ::= "+" ;
OPMinus.        InfixOperator       ::= "-" ;
OPMul.          InfixOperator       ::= "*" ;
OPDiv.          InfixOperator       ::= "/" ;
OPMod.          InfixOperator       ::= "mod" ;
OPPlusD.        InfixOperator       ::= "+." ;
OPMinusD.       InfixOperator       ::= "-." ;
OPMulD.         InfixOperator       ::= "*." ;
OPDivD.         InfixOperator       ::= "/." ;
OPJoin.         InfixOperator       ::= "@" ;
OPXor.          InfixOperator       ::= "^" ;
OPNot.          InfixOperator       ::= "!" ;
OPDef.          InfixOperator       ::= ":=" ;
OPEq.           InfixOperator       ::= "=" ;
OPNeq.          InfixOperator       ::= "<>" ;
OPEqS.          InfixOperator       ::= "==" ;
OPNeqS.         InfixOperator       ::= "!=" ;
OPLt.           InfixOperator       ::= "<" ;
OPEqLt.         InfixOperator       ::= "<=" ;
OPLtD.          InfixOperator       ::= "<." ;
OPEqLtD.        InfixOperator       ::= "<=." ;
OPGt.           InfixOperator       ::= ">" ;
OPEqGt.         InfixOperator       ::= ">=" ;
OPGtD.          InfixOperator       ::= ">." ;
OPEqGtD.        InfixOperator       ::= ">=." ;

OPPrefNot.      PrefixOperator      ::= "!" ;
